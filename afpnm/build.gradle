plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def currentVersionCode = 1
def currentVersionName = '1.0.0'

android {
    namespace 'com.ikurek.android.afpnm'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode currentVersionCode
        versionName currentVersionName

        aarMetadata {
            minCompileSdk = 29
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.ikurek.android'
            artifactId = 'afpnm'
            version = currentVersionName

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            name = 'local'
            url = "${project.buildDir}/repo"
        }
    }

}

dependencies {
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0"))
    implementation("com.squareup.okhttp3:okhttp")

    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-perf-ktx'
}